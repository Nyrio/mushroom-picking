title Mushroom Picking
author nyri0
homepage linktr.ee/nyri0
background_color #10121c
color_palette arnecolors

again_interval 0.06
tween_length 0.06
level_select
level_select_lock
runtime_metadata_twiddling
sprite_size 8x8

text_message_continue [ Press X ]

========
OBJECTS
========

(color palette: https://lospec.com/palette-list/lospec500)

Background
#3388de

Sky
#3388de

PlayerWalkL
#6b2643 #ffa2ac #3388de #94493a #f3a833
.0000000
0000000.
011110..
021210..
0111100.
.000000.
.040000.
33003300

PlayerWalkR
#6b2643 #ffa2ac #3388de #94493a #f3a833
0000000.
.0000000
..011110
..012120
.0011110
.000000.
.000040.
00330033

PlayerClimbL
#6b2643 #ffa2ac #3388de #94493a #f3a833   
.0000000
0000000.
0000000.
0000000.
00000001
10000000
.0000000
33000000

PlayerClimbR
#6b2643 #ffa2ac #3388de #94493a #f3a833   
0000000.
.0000000
.0000000
.0000000
10000000
00000001
0000000.
00000033

PlayerShroomL
#6b2643 #ffa2ac #ec273f #94493a #f3a833
........
.0000000
0000000.
011110..
021210..
0111100.
.0000000
33003300

PlayerShroomR
#6b2643 #ffa2ac #ec273f #94493a #f3a833
........
0000000.
.0000000
..011110
..012120
.0011110
0000000.
00330033

Boulder
#b0a7b8 #9a8ea4 #a499ad #c2bbc9
..0000..
.033000.
03300001
03000001
00000001
00000001
.000001.
..1111..

BoulderBlood
#b0a7b8 #9a8ea4 #a499ad #c2bbc9 #ec273f #6b2643
..0000..
.033000.
03300001
03000001
00000401
40000001
.440044.
5544445.

Earth
#6e4c30

Rock
#48404f #71647d #9a8ea4
20222222
10211111
10211111
00000000
22222022
11111021
11111021
00000000

WallSmall
#8e623e #b0a7b8
00000000
00000010
00100000
00000000
00000100
00000000
01000000
00000000

WallUL
#8e623e #b0a7b8 #9a8ea4
00000000
00000000
00000000
00001100
00011200
00012000
00000000
00000000

WallUR
#8e623e #b0a7b8 #9a8ea4
00000000
00000000
00001110
00001120
00000000
00000000
00000000
00000000

WallDL
#8e623e #b0a7b8 #9a8ea4
00000000
00000000
00000000
00011000
00112000
00012000
00000000
00000000

WallDR
#8e623e #b0a7b8 #9a8ea4
00110000
00112000
00011200
00001200
00000000
00000000
00000000
00000000

GrassSmall
#6e4c30 #5ab552 #52ab49
11111111
21122112
02200220
00000000
00000000
00000000
00000000
00000000

GrassBig
#6e4c30 #5ab552 #52ab49
11111111
11111111
21111112
22111122
02222220
00222200
00000000
00000000

RedShroom
#ec273f White
........
........
..000...
.00010..
0100000.
0000010.
.00000..
..111...

GoldShroom
#f3a833 #de5d3a #e98537
..11111.
.111222.
112200..
22000...
..001111
..01112.
..00.00.
...000..

Sign
#ce9248 #c18133
........
........
.00000..
..00000.
.00000..
...1....
...1....
...1....

RedCursorSeekValid
#ec273f 
00....00
0......0
........
........
........
........
0......0
00....00

RedCursorSeekInvalid
#ec273f 
0......0
........
........
........
........
........
........
0......0

GoldCursorSeekTruValid copy:RedCursorSeekValid
#de5d3a 

GoldCursorSeekTruInvalid copy:RedCursorSeekInvalid
#de5d3a 

GoldCursorSeekRevValid copy:RedCursorSeekValid
#f3a833 

GoldCursorSeekRevInvalid copy:RedCursorSeekInvalid
#f3a833 

CursorLockUL
#ec273f White
00000000
0111...0
011....0
01.....0
0......0
0......0
0......0
00000000

CursorLockUR
#ec273f White
00000000
0...1110
0....110
0.....10
0......0
0......0
0......0
00000000

CursorLockDL
#ec273f White
00000000
0......0
0......0
0......0
01.....0
011....0
0111...0
00000000

CursorLockDR
#ec273f White
00000000
0......0
0......0
0......0
0.....10
0....110
0...1110
00000000

TheVoid
#10121c #2c1e31 #6b2643 #3e3b65  
00000200
01000000
00000000
00000010
00300000
00000000
01000000
00000200

Modified
transparent

(tags)

Falling
transparent

ExpandU
white
...00...
..0..0..
..0..0..
........
........
........
........
........

ExpandD
white
........
........
........
........
........
..0..0..
..0..0..
...00...

ExpandL
white
........
........
.00.....
0.......
0.......
.00.....
........
........

ExpandR
white
........
........
.....00.
.......0
.......0
.....00.
........
........

MarkerU
#ec273f
0.0.0.0.
........
........
........
........
........
........
........

MarkerD
#ec273f
........
........
........
........
........
........
........
.0.0.0.0

MarkerL
#ec273f
0.......
........
0.......
........
0.......
........
0.......
........

MarkerR
#ec273f
........
.......0
........
.......0
........
.......0
........
.......0

=======
LEGEND
=======

Grass = GrassSmall or GrassBig
Wall = WallSmall or WallUL or WallUR or WallDL or WallDR

PlayerWalk = PlayerWalkL or PlayerWalkR
PlayerClimb = PlayerClimbL or PlayerClimbR
PlayerShroom = PlayerShroomL or PlayerShroomR
PlayerMoveL = PlayerWalkL or PlayerClimbL
PlayerMoveR = PlayerWalkR or PlayerClimbR
PlayerMove = PlayerMoveL or PlayerMoveR

GoldCursorSeekTru = GoldCursorSeekTruValid or GoldCursorSeekTruInvalid
GoldCursorSeekRev = GoldCursorSeekRevValid or GoldCursorSeekRevInvalid
GoldCursorSeekValid = GoldCursorSeekTruValid or GoldCursorSeekRevValid
GoldCursorSeekInvalid = GoldCursorSeekTruInvalid or GoldCursorSeekRevInvalid
CursorSeekValid = RedCursorSeekValid or GoldCursorSeekValid
CursorSeekInvalid = RedCursorSeekInvalid or GoldCursorSeekInvalid
GoldCursorSeek = GoldCursorSeekValid or GoldCursorSeekInvalid
CursorSeek = CursorSeekValid or CursorSeekInvalid
CursorLock = CursorLockUL or CursorLockUR or CursorLockDL or CursorLockDR
Cursor = CursorSeek or CursorLock

Player = PlayerMove or Cursor

Ground = Earth or Grass or Boulder or TheVoid or Rock
GroundOrWall = Ground or Wall
Blocker = Earth or Grass or Rock
BlockerOrWall = Blocker or Wall
CanFall = Player or Boulder

Mushroom = RedShroom or GoldShroom
Protected = PlayerShroom or Mushroom or Sign or Boulder or Rock
Expandable = Earth or Grass or Wall or Sky
Erasable = Earth or Grass or Wall or Sky or PlayerMove or Sign or Mushroom or Boulder or Rock
ExpandLR = ExpandL or ExpandR
ExpandUD = ExpandU or ExpandD
Expand = ExpandLR or ExpandUD
MarkerLR = MarkerL or MarkerR
MarkerUD = MarkerU or MarkerD
Marker = MarkerLR or MarkerUD

. = Sky
# = Earth
R = Rock
x = WallSmall
- = GrassSmall
P = PlayerWalkR and Sky
O = Boulder and Sky
Q = Boulder and WallSmall
M = RedShroom and Sky
N = RedShroom and WallSmall
G = GoldShroom and Sky
H = GoldShroom and WallSmall
S = Sign and Sky

=======
SOUNDS
=======

(pickup mushroom)
sfx0 10836305
(invalid action)
sfx1 19489106
(neutral action sound)
sfx2 76151507
(expansion)
sfx3 90043305
(win)
sfx4 42990500

PlayerWalk move left 47967707
PlayerWalk move right 47967707
PlayerClimb move 38689700
Boulder move left 49680707
Boulder move right 49680707
TheVoid create 94995702


================
COLLISIONLAYERS
================

Background
Falling
Sky
Wall
Sign, Mushroom
PlayerMove, PlayerShroom, Earth, Grass, Boulder, BoulderBlood, TheVoid, Rock
Modified
ExpandLR, MarkerLR
ExpandUD, MarkerUD
Cursor

======
RULES
======

(falling boulders can kill the player)
late down [Boulder | PlayerMove] -> [ | BoulderBlood]
(boulders smash mushrooms)
late [Boulder Mushroom] -> [ Boulder]
(walking into the void kills the player)
[> PlayerMove | TheVoid] -> [| TheVoid]

[right PlayerWalkL] -> [right PlayerWalkR]
[left PlayerWalkR] -> [left PlayerWalkL]
[right PlayerClimbL] -> [right PlayerClimbR]
[left PlayerClimbR] -> [left PlayerClimbL]

(the player can't jump)
[up PlayerWalk] -> [PlayerWalk]

(the player can climb)
late [PlayerWalkL Wall] -> [PlayerClimbL Wall] again
late [PlayerWalkR Wall] -> [PlayerClimbR Wall] again
left [> PlayerClimb | No BlockerOrWall] -> [> PlayerWalkL | No BlockerOrWall]
right [> PlayerClimb | No BlockerOrWall] -> [> PlayerWalkR | No BlockerOrWall]
vertical [> PlayerClimbL | No BlockerOrWall] -> [> PlayerWalkL | No BlockerOrWall]
vertical [> PlayerClimbR | No BlockerOrWall] -> [> PlayerWalkR | No BlockerOrWall]

(the player can push boulders)
horizontal [> PlayerMove | Boulder] -> [> PlayerMove | > Boulder]

(picking magic mushrooms starts the rescale power)
late up [PlayerMoveL RedShroom |] -> [PlayerShroomL | RedCursorSeekValid] sfx0 again
late up [PlayerMoveR RedShroom |] -> [PlayerShroomR | RedCursorSeekValid] sfx0 again
late right [| PlayerMoveL GoldShroom |] -> [GoldCursorSeekRevValid | PlayerShroomL | GoldCursorSeekTruValid] sfx0 again
late right [| PlayerMoveR GoldShroom |] -> [GoldCursorSeekRevValid | PlayerShroomR | GoldCursorSeekTruValid] sfx0 again

(rescale implementation)
(gold cursor)
[> GoldCursorSeekTru | ] [ | > GoldCursorSeekRev] -> [> GoldCursorSeekTru | ] [ | < GoldCursorSeekRev]
[> GoldCursorSeekTru] [ > GoldCursorSeekRev] -> [GoldCursorSeekTru] [GoldCursorSeekRev]
[> GoldCursorSeekTru | | < GoldCursorSeekRev] -> [GoldCursorSeekTru | | GoldCursorSeekRev]
(lock cursor after seek)
[> RedCursorSeekValid | Protected] -> [> RedCursorSeekInvalid | Protected]
[> RedCursorSeekInvalid | no Protected] -> [> RedCursorSeekValid | no Protected]
[> GoldCursorSeekTruValid | Protected] -> [> GoldCursorSeekTruInvalid | Protected]
[> GoldCursorSeekTruInvalid | no Protected] -> [> GoldCursorSeekTruValid | no Protected]
[> GoldCursorSeekRevValid | Protected] -> [> GoldCursorSeekRevInvalid | Protected]
[> GoldCursorSeekRevInvalid | no Protected] -> [> GoldCursorSeekRevValid | no Protected]
[action RedCursorSeekInvalid] -> [RedCursorSeekInvalid] sfx1
[action RedCursorSeekValid] -> [CursorLockUL] sfx2
[action GoldCursorSeekTruInvalid] [action GoldCursorSeekRev] -> [GoldCursorSeekTruInvalid] [GoldCursorSeekRev] sfx1
[action GoldCursorSeekRevInvalid] [action GoldCursorSeekTru] -> [GoldCursorSeekRevInvalid] [GoldCursorSeekTru] sfx1
[action GoldCursorSeekTruValid] [action GoldCursorSeekRevValid] -> [CursorLockUL] [CursorLockUL] sfx2
(choose expand direction)
right [> CursorLockUL] -> [CursorLockUR]
right [> CursorLockDL] -> [CursorLockDR]
left [> CursorLockUR] -> [CursorLockUL]
left [> CursorLockDR] -> [CursorLockDL]
down [> CursorLockUL] -> [CursorLockDL]
down [> CursorLockUR] -> [CursorLockDR]
up [> CursorLockDL] -> [CursorLockUL]
up [> CursorLockDR] -> [CursorLockUR]
[> CursorLock] -> [CursorLock]
(spawn expansion)
[action CursorLock Expandable Modified] [PlayerShroom] -> [TheVoid] [PlayerWalkR]
[action CursorLockUL Expandable] [PlayerShroom] -> [Expandable ExpandU ExpandL] [PlayerWalkR] sfx3
[action CursorLockUR Expandable] [PlayerShroom] -> [Expandable ExpandU ExpandR] [PlayerWalkR] sfx3
[action CursorLockDL Expandable] [PlayerShroom] -> [Expandable ExpandD ExpandL] [PlayerWalkR] sfx3
[action CursorLockDR Expandable] [PlayerShroom] -> [Expandable ExpandD ExpandR] [PlayerWalkR] sfx3
[action CursorLock Expandable Modified] -> [TheVoid]
[action CursorLockUL Expandable] -> [Expandable ExpandU ExpandL] sfx3
[action CursorLockUR Expandable] -> [Expandable ExpandU ExpandR] sfx3
[action CursorLockDL Expandable] -> [Expandable ExpandD ExpandL] sfx3
[action CursorLockDR Expandable] -> [Expandable ExpandD ExpandR] sfx3
up [ExpandU ExpandLR | Erasable] -> [ExpandU ExpandLR | ]
down [ExpandD ExpandLR | Erasable] -> [ExpandD ExpandLR | ]
up [Expandable ExpandU ExpandLR | ] -> [Expandable MarkerD ExpandLR | Expandable MarkerU ExpandLR]
down [Expandable ExpandD ExpandLR | ] -> [Expandable MarkerU ExpandLR | Expandable MarkerD ExpandLR]
right [ExpandR MarkerUD | Erasable] -> [ExpandR MarkerUD | ]
left [ExpandL MarkerUD | Erasable] -> [ExpandL MarkerUD | ]
right [Expandable ExpandR MarkerUD | ] -> [Expandable MarkerL MarkerUD | Expandable MarkerR MarkerUD]
left [Expandable ExpandL MarkerUD | ] -> [Expandable MarkerR MarkerUD | Expandable MarkerL MarkerUD]
up [Expandable ExpandU ExpandLR] -> [Expandable MarkerD ExpandLR]
down [Expandable ExpandD ExpandLR] -> [Expandable MarkerU ExpandLR]
right [Expandable ExpandR MarkerUD] -> [Expandable MarkerL MarkerUD]
left [Expandable ExpandL MarkerUD] -> [Expandable MarkerR MarkerUD]
(custom expansion rules)
[Grass MarkerU MarkerLR] -> [GrassBig Modified]
[Grass MarkerD MarkerLR] -> [Earth Modified]
[Wall MarkerU MarkerL] -> [WallUL Modified]
[Wall MarkerU MarkerR] -> [WallUR Modified]
[Wall MarkerD MarkerL] -> [WallDL Modified]
[Wall MarkerD MarkerR] -> [WallDR Modified]
(fallback expansion rule)
[Expandable MarkerUD MarkerLR] -> [Expandable Modified]

(cleanup unsuccessful expansions)
[Expand] -> []

(apply fall to falling objects)
[CanFall Falling] -> [down CanFall]
[Falling] -> []

(mark objects as falling)
late down [ PlayerWalk | no GroundOrWall ]  -> [ PlayerWalk Falling | no GroundOrWall ] again
late down [ Boulder | no Ground ]  -> [ Boulder Falling | no Ground ] again

late [PlayerMove Sign] -> [PlayerMove Sign] sfx4

==============
WINCONDITIONS
==============

Some PlayerMove on Sign

=======
LEVELS
=======

section Intro

message It's a beautiful day to go mushroom picking!

............
............
............
............
P...........
-----......S
#####-------
############

............
............
...........S
.......x----
.......x####
P......x####
--------####
############

............
............
P..O........
----........
####.......S
####.-------
####-#######
############

section Level 1

message Wow, a magic mushroom! How exciting! It grants you the power to rescale some matter.

............
............
............
............
P..M.......S
-----..-----
#####--#####
############

section Level 2

............
............
p.m.m......s
-----.r.----
###.......##
###.......##
###-------##
############

section Level 3

message Time to stretch your imagination! The sky's the limit (or is it?)

............
............
............
......r.....
P..M..r....S
------------
############
############

section Level 4

............
......o.....
.....rrr....
.....r.r....
p..m.......s
-----r.r----
#####rrr####
#####---####

section Level 5

............
.....ooo....
.....rrr....
p..m.......s
-----r.r----
#####r.r####
#####rrr####
#####---####

section Level 6

............
............
...........S
.....M...---
....---x.###
....###x.###
P...xxxx.###
---------###

section Level 7

............
............
...........s
....--------
....#x....##
....##.m..##
p.m.x#--..##
-----###--##


section Level 8

............
p....o......
---x--......
#..x##-.....
#m.x###....s
#--####r.r--
#######rrr##
#######---##

section Level 9

............
p....o......
---x--......
#..x##......
#m.x##.....s
#--###-R.R--
#######RRR##
#######---##

section Level 10

............
.....m......
...ox-......
..--x#.....s
..xxx...r---
p.xx--rrr###
----##---###
############

section Level 11

message Be careful, accidents happen.

............
.....m......
...x--......
...x##......
...x##.o....
p..xxx.o...s
---------.--
#########-##

section Level 12

............
..o....m....
..--o.xr....
...#--x.....
......x.....
p.m...x....s
---r.rrr.r--
###rrrrrrr##

section Level 13

............
.m..........
.--x..o.--..
..#xr.-.rx.s
.mxxr...rx--
.--xr...r###
..#xr...r###
p.xxrrrrr###
---------###

section Level 14

..............
..............
.....m-.......
..ox--#.......
.--x###.......
...x###.......
...xxxx......s
...xx--r.r----
p.mx-##rrr####
----###---####

section Level 15

..............
.........o....
.r.m.ox..r....
.rr---x-rr....
...#xqx#......
...#x-x#.....s
...#xxxx...r--
...#x----..r##
p..xxxxx.m.r##
---------rrr##

section Level 16

................
......o.........
..r.r.r.........
..rrrrr.........
...rrr..........
...rrr..........
.p.rrr....o.....
.-----...rrr....
.xxxxx.....r...s
--xxx----r.r----
##xnx#xnx....m##
##---#---rrr--##

section Level 17

message Holy moly! This one is a rarity!

................
..........o.o...
.........rrrrr..
...........r....
...........r....
..-.o.g....r....
..#-----x..r....
.......#x..r....
p......xx......s
---------r.r.r--
#########rrrrr##
#########-----##

section Level 18

...................
.....o.o.....o.o...
.....rrr.....rrr...
...................
...................
...................
......g..--........
..x------##........
..-x#xxxx##........
..#-#qxhx##........
..xx#---x##........
p.nxxqxxx##.......s
---------##-r...r--
############rrrrr##

message You've collected all the mushrooms, congratulations!


